plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.yl'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    
    compileOnly 'org.projectlombok:lombok'
//    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    // 테스트에서도 QueryDSL Q클래스 사용
    testAnnotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    testAnnotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    testAnnotationProcessor 'jakarta.persistence:jakarta.persistence-api'
}

tasks.named('test') {
    useJUnitPlatform()
}

// QueryDSL 설정
def generated = 'src/main/generated'

// querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [ generated ]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
    delete file(generated)
    delete file('frontend/build') // React 빌드 폴더도 삭제
}

// React 빌드 태스크들
task installDependencies(type: Exec) {
    workingDir 'frontend'
    commandLine 'yarn', 'install'
}

task buildFrontend(type: Exec) {
    dependsOn installDependencies
    workingDir 'frontend'
    commandLine 'yarn', 'build'
}

// 개발용: 프론트엔드 개발 서버 시작
task startFrontend(type: Exec) {
    workingDir 'frontend'
    commandLine 'yarn', 'start'
}

task copyFrontend(type: Copy) {
    dependsOn buildFrontend
    from 'frontend/build'
    into 'src/main/resources/static'
}

// 빌드 시 프론트엔드 포함
build.dependsOn copyFrontend
